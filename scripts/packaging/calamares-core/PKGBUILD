# Maintainer: shivanandvp <shivanandvp@rebornos.org>

pkgname=calamares-core
pkgver=3.3.0_alpha2_1
pkgrel=1
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
pkgdesc='Calamares Installer for RebornOS. This is practically just bare Calamares. Please also install rebornos-calamares-configuration.'
url='https://github.com/RebornOS-Developers/calamares-core'
license=('GPL3')
depends=('qt5-location' 
         'qt5-svg'
         'qt5-tools'
         'qt5-webengine'
         'qt5-xmlpatterns'         
         'qt5ct'
         'qqc2-desktop-style'
         'polkit-qt5'         
         'kconfig'
         'kcoreaddons'
         'kdbusaddons'
         'ki18n'    
         'kpackage'
         'kparts'     
         'kpmcore'
         'plasma-framework'        
         'boost'
         'boost-libs'
         'appstream-qt'
         'libatasmart'
         'libpwquality'
         'parted' 
         'solid'       
         'udisks2' 
         'yaml-cpp'
         'polkit'
         'ckbcomp'
         'adobe-source-han-sans-otc-fonts'
         'adobe-source-han-serif-otc-fonts'         
         )
optdepends=()
makedepends=('extra-cmake-modules'
             'git'
             'qt5-tools') 
provides=('calamares-core')
conflicts=('calamares-core-git' 'calamares-core')
source=("https://github.com/RebornOS-Developers/calamares-core/archive/refs/tags/v${pkgver}.tar.gz")
replaces=('calamares')
md5sums=('SKIP')

prepare() {
    :
}

build() {     
    PROJECT_DIRECTORY="${srcdir}/calamares-core-${pkgver}"
    BUILD_DIRECTORY="$PROJECT_DIRECTORY/build"
    RESOURCE_DIRECTORY="$PROJECT_DIRECTORY/scripts/packaging"
    NUMBER_OF_PROCESSORS="$(nproc)"

    mkdir -p "$BUILD_DIRECTORY" && \
    cmake \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -B "$BUILD_DIRECTORY" -S "$PROJECT_DIRECTORY" && \
    cmake --build "$BUILD_DIRECTORY" --parallel "$NUMBER_OF_PROCESSORS"
}

package() {
    PROJECT_DIRECTORY="${srcdir}/calamares-core-${pkgver}"
    BUILD_DIRECTORY="$PROJECT_DIRECTORY/build"
    RESOURCE_DIRECTORY="$PROJECT_DIRECTORY/scripts/packaging"
    NUMBER_OF_PROCESSORS="$(nproc)"

    # To bypass makepkg's dependency check during build-time
    depends+=('calamares-configuration')

    DESTDIR="${pkgdir}" cmake --install "$BUILD_DIRECTORY" # Install after setting an environment variable

    (
        cd "$RESOURCE_DIRECTORY" && \
        install -Dm 755 run_calamares.sh "${pkgdir}"/etc/calamares/run_calamares.sh
        ln -s /etc/calamares/run_calamares.sh "${pkgdir}"/usr/bin/run_calamares         
    )
    (
        cd "$RESOURCE_DIRECTORY" && \
        find . \
            -mindepth 1 \
            -maxdepth 1 \
            -type f \
            -name '*.policy' \
            -exec \
                install -Dm 755 "{}" "${pkgdir}/usr/share/polkit-1/actions/{}" \;
    )
    (
        cd "$RESOURCE_DIRECTORY" && \
        find . \
            -mindepth 1 \
            -maxdepth 1 \
            -type f \
            -name '*.desktop' \
            -exec \
                install -Dm 755 "{}" "${pkgdir}/usr/share/applications/{}" \;
    )
    (
        cd "$RESOURCE_DIRECTORY" && \
        find . \
            -mindepth 1 \
            -maxdepth 1 \
            -type f \
            -name '*.svg' \
            -exec \
                install -Dm 755 "{}" "${pkgdir}/usr/share/pixmaps/{}" \;
    )
}
